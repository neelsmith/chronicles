import edu.holycross.shot.chron.*
import groovy.xml.MarkupBuilder
import edu.harvard.chs.cite.CiteUrn


String contentType = "text/html"
response.setContentType(contentType)
response.setHeader( "Access-Control-Allow-Origin", "*")

// do some error checking on this ...
String urn = params.urn


String serverUrl = "@tripleserver@"
JGraph jg = new JGraph(serverUrl)


def filumSeq = jg.getRulersForFilum(urn)
def label = jg.getLabel(urn)


StringWriter writer = new StringWriter()
MarkupBuilder html = new MarkupBuilder(writer)

html.html {
    head {
        title("Filum: ${label}")
	link(type : 'text/css',  rel : 'stylesheet',  href : 'css/chron.css', title : 'CSS stylesheet')
    }
    
    body {
        
    	header {
            h1 {
                mkp.yield "Filum: ${label}"
            }
            nav (role: 'navigation') {
                mkp.yield "Chronometer: "
                a(href: "home",  "home")
                mkp.yield " | " 
                a(href: "fila",  "fila")
            }

        }
    	
        article {
            if (filumSeq.size() < 1) {
                p("No rulers found for filum ${label} with SPARQL query")
                blockquote {
                    pre("${jg.jqg.getRulersForFilumQuery(urn)}")
                }
            } else {

                p("Sequence of rulers in filum ${label}")
                ol {
                    filumSeq.each { seq ->
                        li {
                            a(href : "regnalyears?urn=${seq[0]}", seq[1])
                        }
                    }
                }
            }
        }
        footer {
            mkp.yield "INSERTFOOTER"
        }
    }
}


String base = writer.toString()
println base.replaceAll('INSERTFOOTER',"""@htmlfooter@""")


